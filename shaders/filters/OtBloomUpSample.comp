//	ObjectTalk Scripting Language
//	Copyright (c) 1993-2025 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

#version 450

layout(local_size_x=16, local_size_y=16) in;

layout(set=0, binding=0) uniform sampler2D inTexture;
layout(set=1, binding=0, rgba8) uniform writeonly image2D outTexture;

layout(std140, set=2, binding=0) uniform UBO {
    float intensity;
};

void main() {
	ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(outTexture);

	if (coord.x >= size.x || coord.y >= size.y) {
		return;
	}

	vec2 uv = vec2(coord) / vec2(size);
	vec2 halfPixel = vec2(1.0) / vec2(size) / 2.0;

	vec4 sum = (4.0 / 16.0) * texture(inTexture, uv);

	sum += (2.0 / 16.0) * texture(inTexture, uv + vec2(-halfPixel.x, 0.0));
	sum += (2.0 / 16.0) * texture(inTexture, uv + vec2(0.0, +halfPixel.y));
	sum += (2.0 / 16.0) * texture(inTexture, uv + vec2(+halfPixel.x, 0.0));
	sum += (2.0 / 16.0) * texture(inTexture, uv + vec2(0.0, -halfPixel.y));

	sum += (1.0 / 16.0) * texture(inTexture, uv + vec2(-halfPixel.x, -halfPixel.y));
	sum += (1.0 / 16.0) * texture(inTexture, uv + vec2(-halfPixel.x, +halfPixel.y));
	sum += (1.0 / 16.0) * texture(inTexture, uv + vec2(+halfPixel.x, -halfPixel.y));
	sum += (1.0 / 16.0) * texture(inTexture, uv + vec2(+halfPixel.x, +halfPixel.y));

    imageStore(outTexture, coord, sum * intensity);
}
