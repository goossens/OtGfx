//	ObjectTalk Scripting Language
//	Copyright (c) 1993-2025 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

#version 450

layout(local_size_x=16, local_size_y=16, local_size_z=1) in;

layout(set=0, binding=0) uniform sampler2D inTexture;
layout(set=1, binding=0, rgba8) uniform writeonly image2D outTexture;

layout(std140, set=2, binding=0) uniform UBO {
    vec2 offset;
	float brightness;
	float transparency;
};

void main() {
	ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(outTexture);

	if (coord.x >= size.x || coord.y >= size.y) {
		return;
	}

	vec2 uv = vec2(coord) / vec2(size);

	vec4 color;
	color  = texture(inTexture, uv - offset * 4.0f) * 0.05f;
	color += texture(inTexture, uv - offset * 3.0f) * 0.09f;
	color += texture(inTexture, uv - offset * 2.0f) * 0.12f;
	color += texture(inTexture, uv - offset * 1.0f) * 0.15f;
	color += texture(inTexture, uv + offset * 0.0f) * 0.16f;
	color += texture(inTexture, uv + offset * 1.0f) * 0.15f;
	color += texture(inTexture, uv + offset * 2.0f) * 0.12f;
	color += texture(inTexture, uv + offset * 3.0f) * 0.09f;
	color += texture(inTexture, uv + offset * 4.0f) * 0.05f;

	imageStore(outTexture, coord, color * vec4(brightness, brightness, brightness, transparency));
}
