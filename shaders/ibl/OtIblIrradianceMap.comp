//	ObjectTalk Scripting Language
//	Copyright (c) 1993-2025 Johan A. Goossens. All rights reserved.
//
//	This work is licensed under the terms of the MIT license.
//	For a copy, see <https://opensource.org/licenses/MIT>.

#version 450 core
#extension GL_GOOGLE_include_directive : require

#include "constants.glsl"
#include "pbrHelpers.glsl"

layout(local_size_x=16, local_size_y=16, local_size_z=1) in;

layout(set=0, binding=0) uniform samplerCube inTexture;
layout(set=1, binding=0, rg16) uniform writeonly image2DArray outTexture;

void main() {
	int imgSize = 64;
	ivec3 globalId = ivec3(gl_GlobalInvocationID.xyz);

	vec3 N = normalize(toWorldCoords(globalId, float(imgSize)));

	vec3 up = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
	const vec3 right = normalize(cross(up, N));
	up = cross(N, right);

	vec3 color = vec3(0.0);
	uint sampleCount = 0u;
	float deltaPhi = TWO_PI / 360.0;
	float deltaTheta = HALF_PI / 90.0;

	for (float phi = 0.0; phi < TWO_PI; phi += deltaPhi) {
		for (float theta = 0.0; theta < HALF_PI; theta += deltaTheta) {
			// spherical to world space in two steps...
			vec3 tempVec = cos(phi) * right + sin(phi) * up;
			vec3 sampleVector = cos(theta) * N + sin(theta) * tempVec;
			color += texture(inTexture, sampleVector, 0).rgb * cos(theta) * sin(theta);
			sampleCount++;
		}
	}

	imageStore(outTexture, globalId, vec4(PI * color / float(sampleCount), 1.0f));
}
